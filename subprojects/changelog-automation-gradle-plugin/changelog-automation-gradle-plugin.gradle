plugins {
    id 'java-gradle-plugin'
}

configurations {
    jacocoRuntime
}

dependencies {
    api project(':changelog-automation-core')
    implementation gradleApi()

    jacocoRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
    
    testCompile 'junit:junit:4.12'
    testCompile files("$buildDir/testkit")
    testCompile 'org.assertj:assertj-core:3.12.2'
}

task createTestkitFiles {
    def outputDir = file("$buildDir/testkit")
    
    outputs.dir outputDir

    doLast {
	    outputDir.mkdirs()
        String jacocoPath = configurations.jacocoRuntime.asPath.replace('\\', '/')
	    
        file("$outputDir/testkit-gradle.properties").text = "org.gradle.jvmargs=-javaagent:${jacocoPath}=destfile=${buildDir.path.replace('\\', '/')}/jacoco/jacocoTest.exec"
    }
}

check.dependsOn createTestkitFiles

gradlePlugin {
    plugins {
        changelog {
            id = 'org.zkovari.changelog'
            implementationClass = 'org.zkovari.changelog.gradle.api.plugins.ChangelogAutomationPlugin'
        }
    }
}
