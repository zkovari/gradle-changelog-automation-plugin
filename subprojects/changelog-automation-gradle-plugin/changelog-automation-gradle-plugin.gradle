plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'groovy'
}

configurations {
    jacocoRuntime
}

dependencies {
    api project(':changelog-automation-core')
    api project(':changelog-automation-bin')
    implementation gradleApi()

    jacocoRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
    
    testCompile 'junit:junit:4.12'
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
    testCompile files("$buildDir/testkit")
    testCompile 'org.assertj:assertj-core:3.12.2'
}

task createTestkitFiles {
    def outputDir = file("$buildDir/testkit")
    
    outputs.dir outputDir

    doLast {
	    outputDir.mkdirs()
        String jacocoPath = configurations.jacocoRuntime.asPath.replace('\\', '/')
	    
        file("$outputDir/testkit-gradle.properties").text = "org.gradle.jvmargs=-javaagent:${jacocoPath}=destfile=${buildDir.path.replace('\\', '/')}/jacoco/jacocoTest.exec"
    }
}

test.dependsOn createTestkitFiles

pluginBundle {
    website = 'https://github.com/zkovari/gradle-changelog-automation-plugin'
    vcsUrl = 'https://github.com/zkovari/gradle-changelog-automation-plugin'
    tags = ['changelog', 'automation']
}

gradlePlugin {
    plugins {
        changelog {
            id = 'org.zkovari.changelog'
            displayName = 'Changelog automation plugin'
            implementationClass = 'org.zkovari.changelog.gradle.api.plugins.ChangelogAutomationPlugin'
        }
    }
}
